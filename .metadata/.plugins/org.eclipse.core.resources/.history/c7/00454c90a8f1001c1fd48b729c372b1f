@openapi.openedge.export FILE(type="REST", executionMode="singleton", useReturnValue="false", writeDataSetBeforeImage="false").
 
 /*------------------------------------------------------------------------
    File        : CardPile
    Purpose     : 
    Syntax      : 
    Description : 
    Author(s)   : zdelcoco
    Created     : Fri Jun 17 10:45:42 CDT 2022
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.

BLOCK-LEVEL ON ERROR UNDO, THROW.

CLASS CardPile: 
  
  DEFINE PUBLIC  VARIABLE oCardArray AS CLASS Card NO-UNDO EXTENT.    
  DEFINE PRIVATE VARIABLE iLoopIndex   AS INTEGER    NO-UNDO.
  DEFINE PRIVATE VARIABLE oTempCard AS CLASS Card NO-UNDO.  
  
  DEFINE PROPERTY iPileSize AS INTEGER NO-UNDO
    GET.
    PRIVATE SET.       
    
  DEFINE PROPERTY iNumberOfValidCards AS INTEGER NO-UNDO
    GET():
      DEFINE VARIABLE itx AS INTEGER NO-UNDO.
      iNumberOfValidCards = 0.
      DO itx = 1 TO EXTENT(oCardArray):
        IF oCardArray[itx]:cType <> "Empty" THEN 
          iNumberOfValidCards = iNumberOfValidCards + 1.
      END.
      RETURN iNumberOfValidCards.
    END GET.
    PRIVATE SET.     
    
  DEFINE PROPERTY iTopOfStack AS INTEGER NO-UNDO
    GET():
      DEFINE VARIABLE itx AS INTEGER NO-UNDO.
      DO itx = 1 TO EXTENT(THIS-OBJECT:oCardArray):
        IF oCardArray[itx]:cType <> "Empty" THEN NEXT.
        iTopOfStack = itx.
        RETURN iTopOfStack.
      END.
      RETURN EXTENT(THIS-OBJECT:oCardArray).
    END GET.
    PRIVATE SET.    

  CONSTRUCTOR PUBLIC CardPile(ipiNumCards AS INTEGER):    
    THIS-OBJECT:iPileSize = ipiNumCards.    
    EXTENT(oCardArray)  = THIS-OBJECT:iPileSize.    
       
    DO iLoopIndex = 1 TO EXTENT(oCardArray):
      oCardArray[iLoopIndex] = NEW Card("",0).
    END.    
  END CONSTRUCTOR.    
  
  METHOD PROTECTED LOGICAL isLastValidCard(INPUT ipiIndexOfCardInArray AS INTEGER):
    RETURN ipiIndexOfCardInArray = iNumberOfValidCards.
  END METHOD.
  
  METHOD PUBLIC VOID addCard(INPUT ipoCard AS Card):
    THIS-OBJECT:oCardArray[iTopOfStack] = ipoCard.
  END. 
  
  METHOD PUBLIC Card removeCard():
    IF iTopOfStack = 1 THEN RETURN ?.
    
    oTempCard = NEW Card(THIS-OBJECT:oCardArray[iNumberOfValidCards]:cName,
                         THIS-OBJECT:oCardArray[iNumberOfValidCards]:iValue).
                         
    THIS-OBJECT:oCardArray[iNumberOfValidCards]:clearCard().                         
    
    RETURN oTempCard.
  END.  

  DESTRUCTOR PUBLIC CardPile():    
  END DESTRUCTOR.

END CLASS.